
plugins {
    id 'java'
    id 'application'
}

group 'com.mygame'
version '1.0'

application {
    applicationName = 'Character-Bow-Template2'
    mainClass = 'mygame.Main'
    // Mac OS with LWJGL 3 needs to be started with this JVM argument
    if (System.getProperty("os.name").toLowerCase().contains("mac")) {
        applicationDefaultJvmArgs = ['-XstartOnFirstThread']
    }
}

repositories {
    mavenCentral()
}

project.ext {
    jmeVersion = '3.7.0-stable'
}

dependencies {

    // jMonkeyEngine
    implementation 'org.jmonkeyengine:jme3-core:' + jmeVersion
    implementation 'org.jmonkeyengine:jme3-desktop:' + jmeVersion
    implementation 'org.jmonkeyengine:jme3-effects:' + jmeVersion
    
    // Mac OS with LWJGL 3 doesn't allow AWT/Swing
    if (!System.getProperty("os.name").toLowerCase().contains("mac")) {
        runtimeOnly 'org.jmonkeyengine:jme3-awt-dialogs:' + jmeVersion
    }
    
    // Physics
    implementation 'com.github.stephengold:Minie:8.2.0+big4'

    // select one version of LWJGL
    runtimeOnly 'org.jmonkeyengine:jme3-lwjgl:' + jmeVersion  // LWJGL 2.x
    //runtimeOnly 'org.jmonkeyengine:jme3-lwjgl3:' + jmeVersion // LWJGL 3.x

    runtimeOnly 'org.jmonkeyengine:jme3-jogg:' + jmeVersion
    runtimeOnly 'org.jmonkeyengine:jme3-plugins:' + jmeVersion

    //runtimeOnly 'org.jmonkeyengine:jme3-testdata:' + jmeVersion
    runtimeOnly 'org.jmonkeyengine:jme3-testdata:3.4.0-alpha6'
    
    // https://mvnrepository.com/artifact/org.apache.commons/commons-lang3
    implementation 'org.apache.commons:commons-lang3:3.15.0'

}

jar {
    manifest {
        attributes 'Main-Class': application.mainClass
    }
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

wrapper {
    gradleVersion = '8.4'
}